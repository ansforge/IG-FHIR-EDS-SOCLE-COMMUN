map "https://interop.esante.gouv.fr/ig/fhir/eds/StructureMap/Person" = "Person"

/// name = 'Person'
/// title = 'Mapping Patient resource to Person OMOP Domain'
/// status = 'draft'

uses "https://interop.esante.gouv.fr/ig/fhir/eds/StructureDefinition/EDSPatient" as source
uses "https://interop.esante.gouv.fr/ig/fhir/eds/StructureDefinition/Person" alias PersonTable as target

group Person(source src: Patient, target tgt : PersonTable) {
	src.id as srcId -> tgt.person_source_value = srcId;
	src.gender as srcGender then {
	  srcGender -> tgt.gender_concept_id = create('Reference') as tgtReference then {
      srcGender -> translate(srcGender, 'https://interop.esante.gouv.fr/ig/fhir/eds/ConceptMap/Gender', 'code') as genderCode, 
        tgtReference.reference = append('Concept/', genderCode) "ref";
    } "refConcept";
  	srcGender -> tgt.gender_source_value = srcGender "sourceGender";
  } "gender";
  src.birthDate as srcBirthDate then {
    srcBirthDate -> tgt.birth_datetime = srcBirthDate "dateTime";
    srcBirthDate -> tgt.year_of_birth = evaluate(srcBirthDate, substring(0, 4)) "year";
    srcBirthDate -> tgt.month_of_birth = evaluate(srcBirthDate, substring(5, 2)) "month";
  	srcBirthDate -> tgt.day_of_birth = evaluate(srcBirthDate, substring(8, 2)) "day";
  } "birthDate";
}
